<!DOCTYPE html><html lang="en">
<head>
<meta http-equiv="Content-Type" content="text/html; charset=utf-8">
<meta name="topic-type" content="topic">

<meta name="product" content="MAYAUL">
<meta name="release" content="2025">
<meta name="book" content="GeneralUser">
<meta name="component" content="Maya-Scripting">
<meta name="created" content="30/7/2024">
<meta name="generatedby" content="TRIDION">
<meta name="user-comments" content="disable">
<meta name="topicid" content="GUID-11772EDE-7985-4FC1-BA9E-86D952FD5636">
<meta name="language" content="CHS">
<title>测试添加的粒子属性</title>

	<link rel="stylesheet" type="text/css" href="../style/client.css">
	<script type="text/javascript" src="../scripts/client.js"><!--filler--></script>
</head>
<body id="GUID-11772EDE-7985-4FC1-BA9E-86D952FD5636">
<div class="head"><div class="head-block"><div class="button-info"><span id="infoline"> </span></div>
<div class="ancestry"> </div>
<div class="head-text"><h1>测试添加的粒子属性</h1></div>
</div>
</div>

<div class="body"><p class="p" id="GUID-11772EDE-7985-4FC1-BA9E-86D952FD5636__WS73099CC142F48755-3D114B7511841AEFA9F-14D2">由 Autodesk（前 Alias）圣巴巴拉开发中心 Ramsey Harris 编写。</p>
<p class="p" id="GUID-11772EDE-7985-4FC1-BA9E-86D952FD5636__WS73099CC142F48755-3D114B7511841AEFA9F-14D1"> 此脚本 (<a class="xref" href="https://help.autodesk.com/cloudhelp/2025/CHS/Maya-Scripting/files/GUID-11772EDE-7985-4FC1-BA9E-86D952FD5636.htm#GUID-11772EDE-7985-4FC1-BA9E-86D952FD5636__WS17956D7ADBC6E736-40D2EEAA117AE328894-7FFC">dynTestAddAttr.mel</a>) 用于动力学测试。它会为名为 tailSize 的已添加属性设定关键帧，并将其添加到粒子形状。粒子由一个简单的点发射器发射。</p>
<div class="section" id="GUID-11772EDE-7985-4FC1-BA9E-86D952FD5636__WS17956D7ADBC6E736-40D2EEAA117AE328894-7FFC"><h2 class="title sectiontitle">dynTestAddAttr.mel</h2>

<pre class="pre codeblock">
// dynTestAddAttr.mel
//
// Alias Script File
// MODIFY THIS AT YOUR OWN RISK
//
//
// Creation Date: 31 May 1996; Modified 08 January 2000
// Author: rh
//
// Procedure Name:
// dynTestAddAttr
//
// Description:
// Test adding user attributes to a particle shape.
// Create a particle object, set its render type to
// streak, and add a dynamic attribute "tailSize".
// The streak render plug-in will use the attribute
// "tailSize" if it is available.
//
// Input Arguments:
// None.
//
// Return Value:
// Number of errors that occurred in the test.
//
//
//
// ========== dynTestAddAttr ==========
//
// SYNOPSIS
// Test adding user attributes to a particle shape.
// Create a particle object, set its render type to
// streak, and add a dynamic attribute "tailSize".
// The streak render plug-in will use the attribute
// "tailSize" if it is available.
//
global proc int dynTestAddAttr()
{
 // First delete anything that might be left over
 // from a previous test.
 //
 file -force -new;
 currentTime -e 1;
 // Create emitter and particle object.
 //
 emitter -type omni -r 90 -mnd 0 -mxd 0.5 -spd 5 -pos 2 0 2 
 -n myEmitter;
 particle -n myParticle;
 connectDynamic -em myEmitter myParticle;
 // Set the render mode to streak and add a dynamic
 // attribute for the tail size.
 //
 setAttr myParticleShape.particleRenderType 6; // Streak
 addAttr -ln tailSize -dv 4 myParticleShape;
 // Set some keyframes on the dynamic attribute.
 //
 setKeyframe -t 0 -v 0 -at tailSize myParticleShape;
 setKeyframe -t 10 -v 1 -at tailSize myParticleShape;
 setKeyframe -t 20 -v 2 -at tailSize myParticleShape;
 setKeyframe -t 30 -v 5 -at tailSize myParticleShape;
 setKeyframe -t 50 -v 10 -at tailSize myParticleShape;
 setKeyframe -t 70 -v 5 -at tailSize myParticleShape;
 setKeyframe -t 90 -v 1 -at tailSize myParticleShape;
 setKeyframe -t 100 -v 0 -at tailSize myParticleShape;
 // Check for correct tail size at start of test.
 //
 //
 currentTime -e 0;
 int $errors = 0;
 float $tailSize = `getAttr myParticle.tailSize`;
 if ( $tailSize != 0 ) // Warning Magic#
 {
 print( "dynTestAddAttr: Failure: Start of test: The tail "
 + "size ("+ $tailSize + ") should be 0.\n" );
 $errors += 1;
 }
 // Set up the playback options.
 //
 float $frames = 50;
 playbackOptions -min 1 -max $frames -loop once;
 // Time how long it takes to play the scene and then determine the 
 // playback frame rate. Make sure when getting the frame rate
 // that no values are divided by zero.
 //
 float $startTime = `timerX`;
 play -wait;
 float $elapsed = `timerX -st $startTime`;
 float $fps = ($elapsed == 0.0 ? 0.0 : $frames/$elapsed);
 // Check for correct tail size at middle of test.
 //
 $tailSize = `getAttr myParticle.tailSize`;
 if ( ($tailSize &lt; 9.9) || ($tailSize &gt; 10.1) ) // Warning Magic#
 {
 print( "dynTestAddAttr: Failure: Frame 50: The tail size ("
 + $tailSize + ") should be about 10.\n" );
 $errors += 1;
 }
 // Print the frames per second (fps) in the form X.X of subtest.
 //
 print( "dynTestAddAttr: Subtest 1. (" + (int)($fps * 10)/10.0 + 
 " fps)\n");
 // Set up the playback options.
 //
 $frames = 100;
 playbackOptions -min 1 -max $frames -loop once;
 currentTime -e 1;
 // Time how long it takes to play the scene and then determine the 
 // playback frame rate. Make sure when getting the frame rate
 // that no values are divided by zero.
 //
 $startTime = `timerX`;
 play -wait;
 $elapsed = `timerX -st $startTime`;
 $fps = ($elapsed == 0.0 ? 0.0 : $frames/$elapsed);
 // Check for correct tail size at end of test.
 //
 $tailSize = `getAttr myParticle.tailSize`;
 if ( $tailSize &gt; 0.1 )
 {
 print( "dynTestAddAttr: Failure: End of test: The "
 + "tail size (" + $tailSize + ") should be close to 0.\n");
 $errors += 1;
 }
 // If there are no errors, the addAttr passed this test.
 //
 if ( $errors == 0 )
 print( "dynTestAddAttr: Passed. (" );
 else
 print( "dynTestAddAttr: Failed. (" );
 // Print the frames per second (fps) in the form X.X.
 //
 print((int)($fps * 10)/10.0 + " fps)\n");
 // Reset the current time to zero so user can replay the test.
 //
 currentTime -e 1;
 return $errors;
} // dynTestAddAttr //
</pre>

</div>
</div>
<div class="related-links">
<div class="familylinks">
<div class="parentlink"><strong>父主题：</strong> <a class="link" href="https://help.autodesk.com/cloudhelp/2025/CHS/Maya-Scripting/files/GUID-EE07691C-D04C-4C22-8FFD-2E99B3489AE5.htm">示例脚本</a></div>
</div>
</div>

</body>
</html>